'CR1000X Series Datalogger
'Wiring
'Brown -> Power (SW12)
'Orange -> Data (SE1)
'Bare -> Ground (any ground)
'Share SW12 with brown wires for all sensors and put bare wires in any Ground channel
'Put the orange wires in sequential single-ended input channels starting with SE1

Public Flag(5) As Boolean
Alias Flag(1) = Start
Alias Flag(2) = Running
Alias Flag(3) = SaveData15s
Alias Flag(4) = SaveData15m
Alias Flag(5) = Finish
Public Minutes
Public PTemp, Batt_volt

Public SensOutput(4,NumSens)

'Alias elements of sensor output array
Alias SensOutput(1,1) = mV_1: Alias SensOutput(2,1) = VWC_Min_1: Alias SensOutput(3,1) = VWC_SL_1: Alias SensOutput(4,1)=Perm_1
Alias SensOutput(1,2) = mV_2: Alias SensOutput(2,2) = VWC_Min_2: Alias SensOutput(3,2) = VWC_SL_2: Alias SensOutput(4,2)=Perm_2
Alias SensOutput(1,3) = mV_3: Alias SensOutput(2,3) = VWC_Min_3: Alias SensOutput(3,3) = VWC_SL_3: Alias SensOutput(4,3)=Perm_3
Alias SensOutput(1,4) = mV_4: Alias SensOutput(2,4) = VWC_Min_4: Alias SensOutput(3,4) = VWC_SL_4: Alias SensOutput(4,4)=Perm_4
Alias SensOutput(1,5) = mV_5: Alias SensOutput(2,5) = VWC_Min_5: Alias SensOutput(3,5) = VWC_SL_5: Alias SensOutput(4,5)=Perm_5


ConstTable
  Const NumSens = 5 'number of Teros 10 sensors to be measured
  Const NumSensorOutputs = 4
EndConstTable

DataTable (T10data15m, SaveData15m, -1) 'Set table size to # of records, or -1 to autoallocate.
  DataInterval (0, 15, Min, 10)
  CardOut(0,10080) 'Write to microSD card with a week's worth of tables
  Minimum (1, Batt_volt, FP2, False, False)
  Sample (1, PTemp, FP2)
  Sample (NumSens * NumSensorOutputs, SensOutput(), FP2)
EndTable

BeginProg
  Scan (1,Min,0,0)
    PanelTemp (PTemp,60)
    Battery (Batt_volt)

    SW12(SW12_1,1): SW12(SW12_2,1) 'power sensors via SW12
    'Make single-ended voltage measurements
    VoltSe (SensOutput(),NumSens,mV5000,1,1,10000,60,1.0,0)

    Dim counter As Integer
    For counter = 1 To NumSens
      'Convert the raw mV output to VWC in m^3/m^3. Calibrations are taken from the TEROS 10 manual.
      SensOutput(2,counter) = 4.824E-10*SensOutput(1,counter)^3 - 2.278E-6*SensOutput(1,counter)^2 + 3.898E-3*SensOutput(1,counter) - 2.154
      SensOutput(3,counter) = 5.439E-10*SensOutput(1,counter)^3 - 2.731E-6*SensOutput(1,counter)^2 + 4.868E-3*SensOutput(1,counter) - 2.683
      'Apparent dielectric permittivity, non-Meter logger (Eqn 6, p. 14)
      SensOutput(4,counter) = 1.054E-1*EXP(2.827E-3*SensOutput(1,counter))
    Next counter

    SW12(SW12_1,0): SW12(SW12_2,0) 'turn power off
    CallTable T10data15m 'call output table
  NextScan
EndProg

